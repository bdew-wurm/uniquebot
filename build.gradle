plugins {
    id "java"
    id "com.github.ben-manes.versions" version "0.53.0"
}

group "net.bdew.wurm"
version "1.4"

repositories {
    mavenCentral()
    maven { url "https://m2.dv8tion.net/releases" }
    maven { url "https://gotti.no-ip.org/maven/repository" }
    maven { url 'https://jitpack.io' }
}

sourceSets {
    common
    bot
    mod
}

configurations {
    botDeps.extendsFrom botImplementation
    modDeps.extendsFrom modImplementation
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

dependencies {
    botImplementation 'org.apache.commons:commons-lang3:3.20.0'
    botImplementation 'net.dv8tion:JDA:6.1.2'
    botImplementation 'org.slf4j:slf4j-jdk14:2.0.17'
    botImplementation 'com.typesafe:config:1.4.5'
    botImplementation 'org.mariadb.jdbc:mariadb-java-client:3.5.6'
    botImplementation sourceSets.common.output

    modImplementation 'org.gotti.wurmunlimited:server-modlauncher:0.47'
    modImplementation 'com.github.bdew-wurm:bdew_server_mod_tools:2.1.0'
    modImplementation sourceSets.common.output
}

tasks.register('modJar', Jar) {
    from sourceSets.mod.output
    from sourceSets.common.output
    archiveFileName = "mod/${project.name}.jar"
}

tasks.register('botJar', Jar) {
    from sourceSets.bot.output
    from sourceSets.common.output
    archiveFileName = "bot/${project.name}.jar"
    manifest {
        attributes(
                "Main-Class": "net.bdew.wurm.uniquebot.Main",
                "Class-Path": configurations.botDeps
                        .filter { !it.isDirectory() && !it.name.endsWith('.pom') }
                        .collect { "libs/" + it.getName() }.join(' ')
        )
    }
}

tasks.register('dist', Zip) {
    into("mods", {
        into(project.name, {
            from modJar
        })
        from fileTree(dir: 'src/mod/props', include: '*')
    })
    into("libs", {
        from configurations.botDeps
                .filter { !it.isDirectory() && !it.name.endsWith('.pom') }
                .collect()
    })
    from botJar
    from files("application.conf.example", "logging.properties")
    archiveFileName = "${project.name}-${project.version}.zip"
}

tasks.register('docker', Tar) {
    into("libs", {
        from configurations.botDeps
                .filter { !it.isDirectory() && !it.name.endsWith('.pom') }
                .collect()
    })
    from botJar
    archiveFileName = "docker.tar"
}